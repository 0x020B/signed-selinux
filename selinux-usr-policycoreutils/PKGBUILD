# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)                 
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-policycoreutils
_origname=policycoreutils
_release=20110727
pkgver=2.1.0
pkgrel=1
pkgdesc="SELinux userspace (policycoreutils)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsemanage>=2.1.0' 'selinux-usr-libselinux>=2.1.0' 'python2' 'libcap-ng')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/devel/${_origname}-${pkgver}.tar.gz
	restorecond)
sha256sums=('6199b07fd4280e455ea05933cee510b5e09ad3f60b0544382231fe05b787d3f3'
            '8b207474f1a195fe848442c271c8af7e9a1c77e52b43b19165445d68d7d79888')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  sed -i -e "s/shell python -c/shell python2 -c/" "semanage/Makefile"

  sed -i 's|/usr/bin/install|install|g' po/Makefile
  sed -i 's|-Werror||g' setfiles/Makefile
	sed -i '/#define _GNU_SOURCE/d' sandbox/seunshare.c
	sed -i '1 i \
\#define _GNU_SOURCE' sandbox/seunshare.c
	sed -i 's/id.po//' po/Makefile.in
	sed -i 's/id.gmo//' po/Makefile.in
	rm -vf po/id.{po,gmo}

  make
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Move daemons to correct locations
  mv "${pkgdir}/etc/rc.d/init.d/sandbox" \
	"${pkgdir}/etc/rc.d/"
  rm -rf "${pkgdir}/etc/rc.d/init.d/"
  install -m755 "${srcdir}/restorecond" "${pkgdir}/etc/rc.d/"

  # Following are python2 scripts
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2allow"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2why"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/chcat"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/sepolgen-ifgen"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/sbin/semanage"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/lib/python2.7/site-packages/seobject.py"
}
