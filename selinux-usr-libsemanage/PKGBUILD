# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)                 
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-libsemanage
_origname=libsemanage
_release=20110727
pkgver=2.1.0
pkgrel=1
pkgdesc="SELinux userspace (libsemanage including python bindings)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('ustr' 'selinux-usr-libselinux>=2.1.0' 'python2')
conflicts=('selinux-usr-libsemanage-python')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/devel/${_origname}-${pkgver}.tar.gz)
sha256sums=('004c05e32326b1658a85743a950b322d509032dc7c7f652989d7a20ec3652d0f')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # Meant for python2
  sed -i -e "s/shell python -c/shell python2 -c/" "src/Makefile"
  make 
  # Build python wrapper, which is needed for semanage tool
  make pywrap
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pywrap
}
