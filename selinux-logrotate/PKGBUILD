# $Id: PKGBUILD 139122 2011-09-30 02:57:47Z eric $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=selinux-logrotate
_origname=logrotate
pkgver=3.8.1
pkgrel=1
pkgdesc="Tool to rotate system logs automatically with SELinux support"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/logrotate/"
license=('GPL')
groups=('selinux' 'selinux-system-utilities')
depends=('popt' 'selinux-cronie' 'gzip' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
backup=('etc/logrotate.conf')
source=(https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz
        logrotate-3.8.1-noasprintf.patch
        logrotate.conf
        logrotate.cron.daily)
md5sums=('bd2e20d8dc644291b08f9215397d28a5'
         'fc8e4e15211d8aefd431d0e2e96223c4'
         '462a5f364717461537eb2ae6394ad23e'
         '8e23d5d4cc29b1e055b24df87e355cdc')

build() {
  cd "$srcdir/${_origname}-${pkgver}"

  patch -p1 -i "$srcdir"/logrotate-3.8.1-noasprintf.patch

  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  
  make WITH_SELINUX=yes
}

check() {
  cd "$srcdir/${_origname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_origname}-${pkgver}"
  make PREFIX="$pkgdir" install

  install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"
  install -Dm744 "$srcdir/logrotate.cron.daily" "$pkgdir/etc/cron.daily/logrotate"
}
